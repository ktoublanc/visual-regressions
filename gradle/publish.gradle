
publishing {
    publications {
        MYPublication(MavenPublication) {
            artifact javadocJar
            artifact sourcesJar
            from components.java
        }
    }
}

bintray {
    publications = ['MYPublication']
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publish = true
    override = false
    pkg {
        repo = 'Maven'
        name = rootProject.name
        licenses = ['MIT']
        desc = 'Visual Regressions helper tools'
        websiteUrl = 'https://github.com/ktoublanc/visual-regressions'
        issueTrackerUrl = 'https://github.com/ktoublanc/visual-regressions/issues'
        vcsUrl = 'https://github.com/ktoublanc/visual-regressions.git'
        githubRepo = 'ktoublanc/visual-regressions'
        githubReleaseNotesFile = 'README.md'
        publicDownloadNumbers = true

        version {
            afterEvaluate {
                name = project.version
            }
            desc = 'Visual Regressions'
            released  = new Date()
        }
    }
}

artifactory {
    contextUrl = "https://oss.jfrog.org/artifactory"
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = System.getenv('BINTRAY_USER')
            password = System.getenv('BINTRAY_KEY')
            maven = true
        }
        defaults {
            publications ('MYPublication')
        }
    }
}

task deploySelector() {
    afterEvaluate {
        if (project.version.endsWith('-SNAPSHOT')) {
            deploy.finalizedBy artifactoryPublish
        } else {
            deploy.finalizedBy bintrayUpload
        }
    }
}
task deploy() {
    dependsOn deploySelector, check
}

artifactoryPublish.mustRunAfter check
bintrayUpload.mustRunAfter check
